/**
 * application基类
 */

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: rootProject.file('gradle/utils/sign-online.gradle')
apply from: rootProject.file('gradle/plugin/plugin-freeline.gradle')
apply from: rootProject.file('gradle/plugin/plugin-tasktimerecord.gradle')

android {

    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        minSdkVersion MIN_SDK_VERSION as int
    }

    dexOptions {
        javaMaxHeapSize "5g"
        jumboMode true
        preDexLibraries true
        maxProcessCount 8
    }

    aaptOptions {
        cruncherEnabled false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    if (!(BuildContext.isSpeedOrFast())) {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.whenTaskAdded { task ->
    // bugly 符号表由于竞品安全原因，永久禁止
    if (task.name.endsWith("SymtabFile")) {
        task.enabled = false
    }
}

