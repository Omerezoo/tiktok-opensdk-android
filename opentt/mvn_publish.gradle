apply plugin: 'digital.wup.android-maven-publish'
publishing {
    repositories {
        maven {
            url = BuildContext.isSnapshot() ? SNAPSHOT_REPOSITORY_URL :
                    BuildContext.isChina() ? INTERNAL_RELEASE_REPOSITORY_URL : EXTERNAL_RELEASE_REPOSITORY_URL
            credentials {
                username = BuildContext.isChina() ? INTERNAL_USERNAME : EXTERNAL_USERNAME
                password = BuildContext.isChina() ? INTERNAL_PASSWORD : EXTERNAL_PASSWORD
            }
        }
    }
    publications {
        if (project.plugins.hasPlugin("com.android.library")) {
            android.libraryVariants.all { variant ->
                if (variant.name.toLowerCase().contains("release")) {

                    def javadoc = task("${variant.flavorName}Javadoc", type: Javadoc) {
                        source = variant.javaCompile.source
                        exclude '**/BuildConfig.java'
                        exclude '**/R.java'
                        classpath += files(variant.javaCompile.classpath.files) + files(android.bootClasspath)
                    }

                    def javadocJar = project.tasks.create("${variant.flavorName}JavadocJar", Jar) {
                        classifier = 'javadoc'
                        from javadoc.destinationDir
                    }

                    def sourceDirs = variant.sourceSets.collect {
                        it.javaDirectories // Also includes kotlin sources if any.
                    }
                    def sourcesJar = project.tasks.create("${variant.flavorName}SourcesJar", Jar) {
                        classifier = 'sources'
                        from sourceDirs
                    }

                    "${variant.flavorName.capitalize()}AAR"(MavenPublication) {
                        // Gets android component for build variant
                        from components.getByName("android${variant.name.capitalize()}")
                        artifact javadocJar
                        artifact sourcesJar
                        groupId GROUP_ID
                        if (BuildContext.isChina()) {
                            artifactId ARTIFACT_ID
                        } else {
                            artifactId "${ARTIFACT_ID}-overseas"
                        }
                        if (!BuildContext.isSnapshot()) {
                            if (BuildContext.isChina()) {
                                version ARTIFACT_CHINAL_VERSION
                            } else {
                                version ARTIFACT_OVERSEAS_VERSION
                            }
                        } else {
                            if (BuildContext.isChina()) {
                                version "${ARTIFACT_CHINAL_VERSION}-SNAPSHOT"
                            } else {
                                version "${ARTIFACT_OVERSEAS_VERSION}-SNAPSHOT"
                            }
                        }
                        pom.withXml {
                            def depsNode = asNode()["dependencies"][0]
                            if (depsNode != null) {
                                ArrayList list = new ArrayList()
                                depsNode.each { node ->
                                    if (node.groupId.toString().contains(rootProject.name)
                                            && (node.artifactId.toString().contains("bridge")
                                            || node.artifactId.toString().contains("platforms.platform"))) {
                                        list.add(node)
                                    }
                                }
                                for (def node : list) {
                                    depsNode.remove(node)
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}